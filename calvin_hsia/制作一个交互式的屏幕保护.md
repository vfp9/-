# Make an interactive screen saver

作者：Calvin_Hsia

时间：2004年10月4日，星期一，1:24 PM

翻译：xinjie

许多屏幕保护，在存在任何鼠标或键盘动作时都会被终止，但，屏幕保护毕竟是一个程序：它可以针对输入为所欲为。 

下面 40 行示例代码制作了一个 Visual FoxPro 屏幕保护，它是由一个表单和其中的一个 textbox 构成。最后，在资源管理器中，在 TEST.SCR 上右击鼠标，选择安装，以便将其作为一个屏幕保护来进行安装。你甚至可以将 VFP 的 EXE 拷贝成 SCR ，以将其作为一个屏幕保护来安装！

当屏幕保护程序处于活动状态时（参看前面的博客内容：[立刻开始你的屏幕保护程序！](https://github.com/vfp9/My_Translation/blob/main/calvin_hsia/%E7%AB%8B%E5%88%BB%E5%BC%80%E5%A7%8B%E4%BD%A0%E7%9A%84%E5%B1%8F%E5%B9%95%E4%BF%9D%E6%8A%A4%E7%A8%8B%E5%BA%8F.md)），你可以在 textbox 中键入命令，这很像你在 VFP 的命令窗口中来执行。 

如果你做了一个一个带有密码保护的屏幕保护程序，它可以运行在不同的桌面（或位置）：Alt-Tab 允许你在特殊桌面中运行的应用程序间进行切换。

在 textbox 中试着键入下面的内容
```foxpro
!/n taskmgr            
```
 
现在，你可以使用 Alt-Tab 在两个任务间进行切换：任务管理器和屏幕保护程序。 

在 textbox 中，试着依次键入并执行下面的三行代码：
```foxpro
PUBLIC x
x=CREATEOBJECT("word.application")
x.visible=1
```
 
我自己的屏幕保护程序可以随意的显示我收藏的 15000 张照片：使用者（我的家庭成员）可以使它与照片程序相结合（照片程序：来回的看，查找，等等）。
 
```foxpro
ERASE test.*

TEXT TO myvar TEXTMERGE noshow
      PROCEDURE test(parm1,parm2,parm3)
            PUBLIC oScreenSaver

            IF parm1="/p"     &&"preview mode in the config dlog box not supported
                  RETURN
            ENDIF

            oScreenSaver=NEWOBJECT("myform")
            oScreenSaver.show()

            ON KEY LABEL f4 clear events

            READ events
            quit

      DEFINE CLASS myform as Form
            ShowWindow=2 && in desktop

            ADD OBJECT txtCmd as textbox WITH ;
                  width=SYSMETRIC(1)-100,left=100,height=30,;
                  selectonentry=1,;
                  fontsize=14,fontname="verdana"

            PROCEDURE init
                  thisform.Width=SYSMETRIC(1)
                  thisform.Height=SYSMETRIC(2)

            PROCEDURE txtCmd.valid
                  cCmd= ALLTRIM(this.value)

                  IF LEN(cCmd)<0
                        try
                              &cCmd
                              this.value=""
                        CATCH TO oErr
                              this.value=oErr.message
                              this.selectonentry=1
                        ENDTRY
                  ENDIF
      ENDDEFINE
ENDTEXT

STRTOFILE(myvar,"test.prg")
STRTOFILE("screen=off","config.fpw")

MODIFY PROJECT test nowait

_vfp.ActiveProject.Files.Add("test.prg")
_vfp.ActiveProject.Files.Add("config.fpw")
_vfp.ActiveProject.Close

BUILD EXE test FROM test

ERASE config.fpw

*!/n test

COPY FILE test.exe TO test.scr

!start .    && start Explorer in the curdir 
```
