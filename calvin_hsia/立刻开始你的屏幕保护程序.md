# Start your Screen Saver NOW!

作者：Calvin_Hsia

时间：2004年10月4日，星期一，1:00 PM

翻译：xinjie
 
现在，拉斯维加斯发展中国家标准化委员会会议已经结束，这里，我将贴出我参加会议的基本内容和相关代码。 

我将示范如何设置一个简单的 VFP 屏幕保护。要测试它，立刻来调用这些代码是必要的。 

Windows XP 允许在键盘或鼠标没有动作之后的1分钟或更长时间内，运行一个屏幕保护程序。

然而，它并不能用简单的方式立刻开始运行屏幕保护。你可以使用 WIN+L 快捷键来锁定计算机，但随后出现的是系统登录的界面。

你可以在 VFP 的命令窗口运行下面的代码来查看已经注册的屏幕保护程序。
```foxpro
!start  desk.cpl
```
 
这里，有一些代码，可以使当前设定的屏幕保护程序立刻运行。

 ```foxpro
      *立刻开始运行屏幕保护

      #define WM_SYSCOMMAND   0x0112
      #define SC_SCREENSAVE   0xF140

      DECLARE integer GetDesktopWindow IN WIN32API
      DECLARE integer SendMessage IN WIN32API ;
            integer hWnd,;
            integer msg,;
            integer wParam,;
            integer lParam
 
      SendMessage(GetDesktopWindow(),WM_SYSCOMMAND,SC_SCREENSAVE,0)
```
 
当然，你可以用这些代码来创建一个EXE ，并且，在你的开始菜单中为它创建一个快捷方式：

 ```foxpro
*立刻开始屏幕保护程序

TEXT TO myvar TEXTMERGE noshow
#define WM_SYSCOMMAND   0x0112
#define SC_SCREENSAVE   0xF140

DECLARE integer GetDesktopWindow IN WIN32API
DECLARE integer SendMessage IN WIN32API ;
      integer hWnd,;
      integer msg,;
      integer wParam,;
      integer lParam

SendMessage(GetDesktopWindow(),WM_SYSCOMMAND,SC_SCREENSAVE,0)
ENDTEXT

STRTOFILE(myvar,"SSaver.prg")
STRTOFILE("screen=off","config.fpw")

MODIFY PROJECT SSaver nowait

_vfp.ActiveProject.Files.Add("SSaver.prg")
_vfp.ActiveProject.Files.Add("config.fpw")
_vfp.ActiveProject.Build("ssaver.exe")
_vfp.ActiveProject.Close

*!/n SSaver

*这里在开始菜单创建一个指向 EXE 的快捷方式

LOCAL oSh as WScript.Shell
oSh=CREATEOBJECT("WScript.Shell")

cStartMenu=oSh.SpecialFolders("StartMenu")

LOCAL oScut as WSCRIPT.WshShortcut
oScut=osh.CreateShortcut(cStartMenu+"\ssaver.lnk")

oScut.Description="VFP Screen saver starter"
oScut.Hotkey="CTRL+ALT+SHIFT+S"
*oScut.IconLocation=
oScut.TargetPath=SYS(5)+CURDIR()+"ssaver.exe"
oScut.Save
```

你可以使用注册表或 WMI 来列出屏幕保护程序：

```foxpro
*使用 WMI 列出屏幕保护程序

strComputer = "."

objWMIService = GetObject("winmgmts:\\" + strComputer + "\root\cimv2")

colItems = objWMIService.ExecQuery("Select * from Win32_Desktop")

For Each objItem in colItems
    ? "Screen Saver Secure: " + TRANSFORM(objItem.ScreenSaverSecure)
    ??" ",objItem.ScreenSaverExecutable," ",objItem.ScreenSaverTimeout
Next
```

这个代码使用 SystemParametersInfo 来控制屏幕保护程序。如果你的应用程序需要超过屏幕保护运行的时间，或者你想完全控制应用，或者你想在没有屏幕保护的时候显示 UI ，你可以禁用它。

 ```foxpro
#define SPI_GETSCREENSAVETIMEOUT    0x000E
#define SPI_SETSCREENSAVETIMEOUT    0x000F
#define SPI_GETSCREENSAVEACTIVE     0x0010
#define SPI_SETSCREENSAVEACTIVE     0x0011
#define SPI_GETSCREENSAVERRUNNING 0x0072
#define SPI_SETSCREENSAVERRUNNING   0x0061
#define SPI_SCREENSAVERRUNNING     SPI_SETSCREENSAVERRUNNING
#define SPIF_SENDWININICHANGE 0x0002

DECLARE integer SystemParametersInfo IN WIN32API ;
      integer uiAction,;
      integer uiParam,;
      integer @ pvParam ,;
      integer fWinini

nval=0     

SystemParametersInfo(SPI_GETSCREENSAVEACTIVE,0,@nVal,0)

?"Screen Saver is"+IIF(nval=0," Not","")+" active.",DATETIME()
 

fEnable =IIF(MESSAGEBOX("Enable screen saver?",4)=6,1,0)    && 6=YES

SystemParametersInfo(SPI_SETSCREENSAVEACTIVE,fEnable ,0,0)

nval=0     

SystemParametersInfo(SPI_GETSCREENSAVEACTIVE,0,@nVal,0)

?"Screen Saver is"+IIF(nval= 0," Not","")+" active.",DATETIME()

ns=SECONDS()

DO WHILE SECONDS()-ns < span>60 AND INKEY(1)=0
      WAIT WINDOW NOWAIT TRANSFORM(INT(SECONDS()-ns))
ENDDO
```
